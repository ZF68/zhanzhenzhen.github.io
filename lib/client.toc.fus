fus 2.3.0
import "./shared.manifest" all

client: import "site/client"
ui: client.ui
main: import "./client.main" all

compactSwitch'export: false

fillBlogs'export: blogs ->
    main.toc.empty()
    blogs.forEach(blog ->
        main.toc.add(ui.Group(
            {
                width: 1
                height: "3rem"
                padding: ui.Padding("0.5rem")
                backgroundPaint: main.COLOR_LEFT_ITEM_BACKGROUND
                pointer: "link"
                hoverStyles: {backgroundPaint: main.COLOR_LEFT_ITEM_HOVER_BACKGROUND}
                activeStyles: {backgroundPaint: main.COLOR_LEFT_ITEM_ACTIVE_BACKGROUND}
                click: <>
                    compactToggle() if main.inCompactMode
                    client.setUri("/" + blog.id)
            }
            ui.Element(blog.htmlTitle, {width: 0.93, wrapping: "wrap", lineHeight: 1.2})
        ))
    )
    refresh()

refresh'export: <>
    main.toc.getChildren().forEach((item, index) ->
        if index < main.tocLimit
            item.show()
        else
            item.hide()
    )

compactToggle'export: (quick: false) ->
    compactSwitch: not self
    newTranslate: [compactSwitch ? 0.333333 | 0, 0]
    if quick
        main.mainBody.setTranslate(newTranslate)
    else
        main.mainBody.animate{
            to: {translate: newTranslate}
            duration: 300
        }
