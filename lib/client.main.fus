fus 2.0.6
import "babel-polyfill"
import "./shared.manifest" all

# Modify these 2 variables to match yours.
username: "zhanzhenzhen"
repo: "zhanzhenzhen.github.io"

shared: import "site/shared"
#import "./shared.main"
client: import "site/client"

marked: import "marked"
#manuals: require("./client.manuals.coffee")
ui: client.ui

apiBase: "https://api.github.com"

docsObj: {}
originalTitle: document.title

parseElement: s -> DOMParser().parseFromString(s, "application/xml").documentElement

ui.setRem(0.02)

contentElement: ui.Element(
    {
        width: 0.8
        lineHeight: 1.5
        selectable: true
        pointer: "auto"
    }
)
contentDom: contentElement.raw
ui.root.add(
    ui.Stack(
        {
            width: "auto"
            aspectRatio: 1.25
            horizontal: true
        },
        ui.Stack{
            width: 0.2
            childSpacing: "0.3rem"
            padding: ui.Padding(0, "2rem", 0, 0)
        } as leftCol,
        ui.Stack(
            {
                childAlignment: "start"
                overflow: "scroll"
                childSpacing: "1rem"
                padding: "0.5rem"
            },
            contentElement,
            ui.Text("评论", {fontSize: 2, backgroundPaint: "rgb(224,255,224)"}),
            ui.Text("以下是网友评论。不代表本人支持其中的观点。请勿发表不良内容，否则一经发现将被删除\
            （如发现请向本人举报）。", {fontSize: 0.8, wrapping: "wrap"}),
            ui.Stack{
                childAlignment: "start"
                height: "auto"
                childSpacing: "0.5rem"
            } as commentsElement,
            ui.Element{height: "1rem"},
            ui.Text("
                点击“添加评论”后，请在标题栏输入文章的ID，文章ID为URL中的路径名（不包括“/”）。\
                例如，URL为“https://zhanzhenzhen.github.io/2016_01_01”，则ID为“2016_01_01”。\
                如果是为主页添加评论，则不用输入ID。
            ", {wrapping: "wrap", fontSize: 0.9}),
            ui.Button("添加评论", {
                fontSize: 1.3
                padding: ui.Padding("1rem", "0.5rem")
                click: <>
                    location.href: "https://github.com/\(username)/\(repo)/issues/new"
            }),
            ui.Button("All Issues", {
                fontSize: 0.9
                click: <>
                    location.href: "https://github.com/\(username)/\(repo)/issues"
            })
        )
    )
)
Promise.all(client.text.map(doc ->
    if (doc.filename.endsWith(".md") or doc.filename.endsWith(".markdown"))
    and doc.content.startsWith("<script ")
        pos: doc.content.search("</script>")
        metaString: doc.content.substr(0, pos + "</script>".length)
        metaElement:
            DOMParser().parseFromString(metaString, "text/html")
            .querySelector("script[type=\"application/ld+json\"]")
        fileType: "markdown"
    (
        if metaElement'ok
            shared.jsonLd.compact(
                JSON.parse(metaElement.textContent),
                {"@context": "http://schema.org/"}
            )
        else
            Promise.resolve{}
    ).then(meta ->
        candidateId: doc.filename.substr(0, doc.filename.lastIndexOf("."))
        {
            title: meta.headline ifnull "no title"
            time: Date(meta.dateCreated ifnull "2010-01-02T00:00Z")
            type: fileType
            id: match candidateId
                ""     ? null
                "home" ? ""
                |        candidateId
            content: doc.content
        }
    )
)).then(rawDocs ->
    docs: rawDocs.filter(m -> m.type'ok and m.id'ok)
    docs.forEach doc ->
        docsObj.(doc.id): doc
    groups: docs..group(m ->
        if m.id = ""
            "home"
        else if m.id.match(r"^\d\d\d\d_\d\d_\d\d($|_)")'ok
            "blog"
        else
            "article"
    )
    homeGroup: groups..singleOrNull(m -> m.0 = "home")
    blogGroup: groups..singleOrNull(m -> m.0 = "blog")

    if homeGroup'ok
        leftCol.add(ui.Text(homeGroup.1.0.title, {
            padding: ui.Padding("0.5rem")
            backgroundPaint: "rgb(224,224,224)"
            pointer: "link"
            click: <> client.setUri("/")
        }))

    if blogGroup'ok
        blogGroup.1..sortDescending(m -> m.time).forEach(blog ->
            date: blog.time
            dateStr: date.getUTCFullYear()..pad(4) + "-"
                + (date.getUTCMonth() + 1)..pad(2) + "-"
                + date.getUTCDate()..pad(2)
            leftCol.add(ui.Stack(
                {
                    height: "auto"
                    padding: ui.Padding("0.5rem")
                    backgroundPaint: "rgb(224,224,224)"
                    pointer: "link"
                    click: <> client.setUri("/" + blog.id)
                },
                ui.Text(blog.title),
                ui.Text(dateStr, {fontSize: 0.7})
            ))
        )

    oldPath: null
    uriUpdateHandler: <>
        path: decodeURI(location.pathname)
        if path ≠ oldPath

            contentElement.empty()
            commentsElement.empty()

            strippedPath: path.substr(1)
            if strippedPath.endsWith("/")
                strippedPath: strippedPath.substr(0, strippedPath.length - 1)
            doc: docsObj.(strippedPath)

            if doc'ok
                document.title: doc.title
                if doc.type = "markdown"
                    htmlString: marked.parse(doc.content)
                    contentDom.innerHTML: parseElement("
                        <div xmlns="http://www.w3.org/1999/xhtml">
                            \(htmlString)
                        </div>
                    ").innerHTML
                else
                    contentDom.textContent: doc.content
            else
                document.title: ""
                contentDom.textContent: ""

            web.jsonGet("\(apiBase)/search/issues?q=\(encodeURIComponent(doc.id))+\
            in:title+-label:blocked+repo:\(username)/\(repo)&sort=created")
            .then(response ->
                issues: response.body.items
                issues.forEach issue ->
                    commentsElement.add(ui.Stack(
                        {
                            childAlignment: "start"
                            height: "auto"
                            childSpacing: "0.3rem"
                        },
                        ui.Text(issue.user.login + ":"),
                        ui.Text(issue.body, {wrapping: "wrap"}),
                        ui.Stack{
                            childAlignment: "start"
                            height: "auto"
                        } as subcommentsElement,
                        ui.Button("回复", {
                            fontSize: 0.8
                            click: <>
                                location.href: issue.html_url
                        })
                    ))
                    web.jsonGet("\(apiBase)/repos/\(username)/\(repo)/issues/\(issue.number)\
                    /comments?per_page=100")
                    .then(commentsResponse ->
                        comments: commentsResponse.body
                        comments.forEach comment ->
                            subcommentsElement.add(ui.Text(comment.body, {
                                fontSize: 0.85
                                padding: ui.Padding("1rem", "0rem", "0rem", "0rem")
                                wrapping: "wrap"
                            }))
                    )
            ).catch(<>
            )

            oldPath: path

    uriUpdateHandler()
    client.onUriChange(uriUpdateHandler)
)
