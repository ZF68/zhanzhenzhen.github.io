fus 2.0.5
import "babel-polyfill"
import "./shared.manifest" all

shared: import "site/shared"
import "./shared.main"
client: import "site/client"

jsonld: import "json-ld"
#manuals: require("./client.manuals.coffee")
ui: client.ui

client.autoCloseStartup: false

parseElement: s -> DOMParser().parseFromString(s, "application/xml").documentElement
articleTitle: element ->
    element.getElementsByTagName("title").0.textContent

books: [
    "programming"
]

blogs: [
    "所谓的曲面电视"
    "屏幕分辨率多少才完美"
    "玄米抹茶的做法"
    "苹果手表"
    "AlphaGo人机大战之预测"
]

ui.setRem(0.02)

loads:
    books.map(book -> {type: "book", filename: book + ".xhtml", name: book})
    .concat(blogs.map(blog -> {type: "blog", filename: blog + ".xhtml", name: blog}))
Promise.all(loads.map(item -> web.get(item.filename)))
.then(x ->
    x.forEach (item, index) ->
        load: loads.(index)
        load.text: item.body
        load.element: parseElement(item.body)
        load.title: articleTitle(load.element)
    Promise.all(loads.map(item ->
        jsonld.compact(
            JSON.parse(item.element.querySelector("script[type=\"application/ld+json\"]").textContent),
            {"@context": "http://schema.org/"}
        )
    ))
).then(x ->

    client.closeStartup()

    x.forEach (data, index) ->
        load: loads.(index)
        load.time: data.dateCreated

    contentElement: ui.Element(
        {
            width: 0.8
            overflow: "scroll"
            lineHeight: 1.5
            selectable: true
            pointer: "auto"
            padding: "0.5rem"
            backgroundPaint: "rgb(255,255,255)"
        }
    )
    ui.root.add(
        ui.Stack(
            {
                width: "auto"
                aspectRatio: 1.25
                horizontal: true
                backgroundPaint: "rgb(255,255,255)"
                padding: ui.Padding("1rem", 0, "1rem", 0)
            },
            ui.Stack(width: 0.2, childSpacing: "0.3rem", padding: ui.Padding(0, "2rem", 0, 0)) as leftCol,
            contentElement,
            ui.Stack(width: 0.2, childSpacing: "0.3rem", padding: ui.Padding(0, "2rem", 0, 0)) as rightCol
        )
    )
    loads.filter(m -> m.type = "book").forEach(book ->
        leftCol.add(ui.Text(book.title, {
            padding: ui.Padding("0.5rem")
            backgroundPaint: "rgb(224,224,224)"
            pointer: "link"
            click: <> client.setUri("/" + book.name)
        }))
    )
    loads.filter(m -> m.type = "blog").forEach(blog ->
        date: Date(blog.time)
        dateStr: date.getUTCFullYear()..pad(4) + "-"
            + (date.getUTCMonth() + 1)..pad(2) + "-"
            + date.getUTCDate()..pad(2)
        rightCol.add(ui.Stack(
            {
                height: "auto"
                padding: ui.Padding("0.5rem")
                backgroundPaint: "rgb(224,224,224)"
                pointer: "link"
                click: <> client.setUri("/" + blog.name)
            },
            ui.Text(blog.title),
            ui.Text(dateStr, {fontSize: 0.7})
        ))
    )

    oldPath: null
    uriUpdateHandler: <>
        path: decodeURI(location.pathname)
        if path ≠ oldPath

            contentElement.empty()

            if path = "/"
                window.contentElement: contentElement
                contentElement.raw.innerHTML: loads.0.element.getElementsByTagName("body").0.innerHTML
            else
                contentElement.raw.innerHTML: loads..single(m ->
                    m.filename = path.substr(1) + ".xhtml"
                ).element.getElementsByTagName("body").0.innerHTML

            oldPath: path

    uriUpdateHandler()
    client.onUriChange(uriUpdateHandler)
)

window.loads: loads
