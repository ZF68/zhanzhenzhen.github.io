fus 2.3.0
import "babel-polyfill"
import "./shared.manifest" all

# Modify these 2 variables to match yours.
username: "zhanzhenzhen"
repo: "zhanzhenzhen.github.io"

shared: import "site/shared"
import "./shared.main"
client: import "site/client"

markdownIt: import "markdown-it"
emoji: import "markdown-it-emoji"
twemoji: import "twemoji"
hljs: import "highlight.js"
#manuals: require("./client.manuals.coffee")
ui: client.ui
lit: client.lit

$toc: import "./client.toc" all
$button: import "./client.button" all
COLORS: import "./client.colors" all
search: import "./client.search"

apiBase: "https://api.github.com"

docsObj: {}
originalTitle: document.title

md: markdownIt{
    html: true
    xhtmlOut: true
    highlight: (str, lang) ->
        if hljs.getLanguage(lang)'ok
            try
                hljs.highlight(lang, str).value
            catch
                ""
        else
            ""
}
md.use(emoji)
md.renderer.rules.emoji: (token, idx) ->
    r: twemoji.parse(token.(idx).content)

    # This emoji plugin isn't compatible with XHTML, so we must convert it to XHTML
    matched: r.match(r"^(<img\b[^>]+[^/])>$")
    r: matched'ok ? matched.1 + "/>" | token.(idx).content

    r

parseElement: s -> DOMParser().parseFromString(s, "application/xml").documentElement

startSearch: <>
    searchResultsCursor: 0
    searchResults: search(searchBox.getSource(), blogs)
    $toc.fillBlogs(searchResults..portion(searchResultsCursor, tocLimit))

showFirstScene: <>
    viewportSize: ui.getSize()
    firstScene.setPaint("rgb(255,255,255)").setBackgroundPaint("rgb(255,255,255)")
    firstSceneTitle.setTranslate(0, 0)
    firstSceneHint.setTranslate(0, ui.RemLength(-viewportSize.y * 0.1)).setOpacity(0)
    sceneBox.showScene(firstScene, ui.transition.dissolve, 300).then(<>
        firstScene.animate{
            keyframes: {
                "0.167": {paint: "rgb(50,50,50)", backgroundPaint: "rgb(255,255,255)"}
                "0.5": {backgroundPaint: "rgb(255,192,192)"}
                "0.667": {backgroundPaint: "rgb(255,192,192)"}
                "1": {paint: "rgb(50,50,50)", backgroundPaint: "rgb(255,216,192)"}
            }
            duration: 3600
        }
        firstSceneHint.animate{
            keyframes: {
                "0.5": {opacity: 0, translate: [0, ui.RemLength(-viewportSize.y * 0.1)]}
                "1": {opacity: 1, translate: [0, 0]}
            }
            duration: 1200
            timingFunction: "ease-out"
        }
    )

inCompactMode'export: false
header'export: null
contentElement'export: null
contentDom'export: null
leftCol'export: null
toc'export: null
blogs'export: null
searchResults'export: null
searchResultsCursor'export: 0
commentsElement'export: null
docs'export: null
currentDoc'export: null
tocLimit'export: null
mainBody'export: null
firstScene'export: null
secondScene'export: null
mainScene'export: null
scrollElement'export: null
sceneBox'export: null
wheelEffective'export: true

ui.setRem ui.GmRem(client.browser.deviceType = "handheld" ? 0.032 | 0.016)

docs: client.text
.filter(doc ->
    not doc.filename.startsWith(".") and (
        doc.filename.endsWith(".md") or doc.filename.endsWith(".markdown")
    )
).map(doc ->
    head: doc.content.substr(0, 80).trim()
    head: head.match(r"^(?:#* )?([^\r\n]*)").1
    renderedHead: md.render(head)
    renderedHead: renderedHead.match(r"<p>(.*)</p>").1
    id: doc.filename.substr(0, doc.filename.lastIndexOf("."))
    if id = "home"
        id: ""
        category: "home"
    else
        matched: id.match(r"^(\d\d\d\d)_(\d\d)_(\d\d)($|_)")
        if matched'ok
            category: "blog"
            time: "\(matched.1)-\(matched.2)-\(matched.3)"
        else
            category: "article"
    {
        id: id
        category: category
        title: head
        htmlTitle: renderedHead
        time: time
        content: doc.content
    }
)

contentElement: ui.Element(
    {
        width: 1
        lineHeight: 1.5
        selectable: client.browser.deviceType = "computer"
        pointer: "auto"
    }
)
contentDom: contentElement.raw
contentDom.id: "content"
ui.root
.setFontFamily("
    "Helvetica Neue", Helvetica, Arial, "Lucida Grande", "Lucida Sans Unicode", \
    "PingFang SC", "Microsoft YaHei UI", "Microsoft YaHei", SimHei, sans-serif
")
.setPaint("rgb(0,0,0)")
.setBackgroundPaint("rgb(255,255,255)")
.setPointerDefaultEnabled(client.browser.deviceType = "computer")
firstScene: ui.Board(
    {backgroundPaint: "rgb(255,255,255)", paint: "rgb(255,255,255)"}
    ui.Stack(
        {y: 0.4}
        ui.Text({fontSize: 1, opacity: 0.2}, lit.firstSceneTitleAnnotation)
        ui.Text({fontSize: 3.5}, lit.firstSceneTitle)
    ) as firstSceneTitle
    ui.Text(lit.wheelHint, {y: 0.6}) as firstSceneHint
)
secondScene: ui.Board({backgroundPaint: "rgb(248,232,210)", paint: "rgb(50,50,50)"}, ui.Stack(
    {childAlignment: "center", width: 0.5, childSpacing: "2rem"}
    ui.Image(
        {
            width: 1
            border: {width: "0.2rem", paint: "rgb(96,128,112)"}
            borderRadius: "2rem"
        }
        "/childhood-computer.jpg"
    )
    ui.Text(
        {wrapping: "wrap", width: 1, lineHeight: 1.35}
        lit.secondSceneText
    )
    ui.Text(lit.wheelHint)
) as secondSceneContent)
mainScene: ui.Board({backgroundPaint: COLORS.outerBackground}, ui.Stack(
    {
        width: "auto"
        height: 1
        aspectRatio: 1.33333
        horizontal: true
        backgroundPaint: COLORS.innerBackground
        paint: COLORS.text
    }
    ui.Stack(
        {
            width: 0.22
            height: 1
            childSpacing: "1rem"
            padding: ui.Padding("0.5rem", "2rem", 0, "1rem")
        }
        ui.Stack(
            {
                width: 1
                childSpacing: "0.3rem"
            }
            ui.Text(lit.prelude, {
                padding: ui.Padding("0.5rem")
                backgroundPaint: COLORS.leftItemBackground
                pointer: "link"
                hoverStyles: {backgroundPaint: COLORS.leftItemHoverBackground}
                activeStyles: {backgroundPaint: COLORS.leftItemActiveBackground}
                click: <>
                    client.localData.homeLoaded: false
                    client.saveLocalData()
                    scrollElement.raw.scrollTop: 0
                    ui.root.setWheelTarget(null)
                    $toc.compactToggle(true) if $toc.compactSwitch
                    showFirstScene()
                    client.setUri("/")
            })
            ui.Text(lit.home, {
                padding: ui.Padding("0.5rem")
                backgroundPaint: COLORS.leftItemBackground
                pointer: "link"
                hoverStyles: {backgroundPaint: COLORS.leftItemHoverBackground}
                activeStyles: {backgroundPaint: COLORS.leftItemActiveBackground}
                click: <>
                    $toc.compactToggle() if inCompactMode
                    client.setUri("/")
            })
        )
        ui.Stack(
            {
                horizontal: true
                width: 1
                height: "2rem"
                childSpacing: "0.3rem"
            }
            ui.TextBox{
                width: 0.8 # Ideally there's no need to set it, but IE11 will render wrong if it's unset.
                grow: 1
                shrink: 1
                border: {width: "0.0933333em", paint: COLORS.text}
                commitTextInput: <> startSearch()
            } as searchBox
            $button.Button(lit.search, {click: <> startSearch()})
        )
        ui.Stack(
            {
                horizontal: true
                width: 1
                height: "2rem"
                childSpacing: "0.3rem"
            }
            $button.Button(lit.searchPrevious, {click: <>
                searchResultsCursor: Math.max(self - tocLimit, 0)
                $toc.fillBlogs(searchResults..portion(searchResultsCursor, tocLimit))
            }) as previousButton
            $button.Button(lit.searchNext, {click: <>
                searchResultsCursor: Math.max(Math.min(self + tocLimit, searchResults.length - tocLimit), 0)
                $toc.fillBlogs(searchResults..portion(searchResultsCursor, tocLimit))
            }) as nextButton
        )
        ui.Stack{
            width: 1
            height: "35rem"
            childSpacing: "0.3rem"
        } as toc
    ) as leftCol
    ui.Stack(
        {
            childAlignment: "start"
            overflow: "hidden"
            childSpacing: "1rem"
            padding: ui.Padding("0.5rem", "2rem", "0.5rem", "0rem")
            width: 0.78
            height: 1
        }
        ui.Stack(
            {
                horizontal: true
                childSpacing: "1rem"
            }
            ui.Text() as headerTime
            $button.Button(lit.toc, {click: <>
                $toc.compactToggle() if inCompactMode
            }) as headerTocButton
        ) as header
        contentElement
        ui.Text(lit.comments, {fontSize: 2})
        ui.Text(lit.commentsDisclaimer, {fontSize: 0.8, wrapping: "wrap", width: 1, lineHeight: 1.35})
        ui.Stack{
            childAlignment: "start"
            width: 1
            height: "auto"
            childSpacing: "0.5rem"
        } as commentsElement
        ui.Element{height: "1rem"}
        ui.Stack(
            {
                horizontal: true
                childSpacing: "0.5rem"
                childAlignment: "center"
            }
            $button.Button(lit.addComment, {
                fontSize: 1.3
                padding: ui.Padding("1rem", "0.5rem")
                click: <>
                    client.openLink("https://github.com/\(username)/\(repo)/issues/new", true)
            })
            ui.Text() as addCommentHint
        )
        ui.Element{height: "1rem"}
        ui.Stack(
            {
                horizontal: true
                childSpacing: "0.5rem"
            }
            $button.Button("GitHub", {
                fontSize: 0.9
                click: <>
                    client.openLink("https://github.com/\(username)/\(repo)", true)
            })
            $button.Button("All Issues", {
                fontSize: 0.9
                click: <>
                    client.openLink("https://github.com/\(username)/\(repo)/issues", true)
            })
            $button.Button("English", {
                fontSize: 0.9
                click: <>
                    client.localMeta.language: "en"
                    client.saveLocalMeta()
                    location.reload()
            })
            $button.Button("中文", {
                fontSize: 0.9
                click: <>
                    client.localMeta.language: "zh"
                    client.saveLocalMeta()
                    location.reload()
            })
        )

        # This can't be replaced with a bottom-padding in the container, because bottom-padding
        # will be ignored by some browsers. Also, we must set a non-zero width, otherwise Firefox
        # will still ignore it.
        ui.Element{width: "1rem", height: "1rem"}
    ) as scrollElement
) as mainBody)
sceneBox: ui.SceneBox(firstScene, secondScene, mainScene)
ui.root.add sceneBox

docs.forEach doc ->
    docsObj.(doc.id): doc

blogs: docs.filter(m -> m.category = "blog")..sortDescending(m -> m.time)
searchResults: blogs
###
blogGroup.1..sortDescending(m -> m.time).forEach(blog ->
    date: blog.time
    dateStr: date.getUTCFullYear()..pad(4) + "-"
        + (date.getUTCMonth() + 1)..pad(2) + "-"
        + date.getUTCDate()..pad(2)
    toc.add(ui.Stack(
        {
            width: 1
            height: "4rem"
            padding: ui.Padding("0.5rem")
            backgroundPaint: "rgb(224,224,224)"
            pointer: "link"
            click: <> client.setUri("/" + blog.id)
        }
        ui.Text(blog.title, {wrapping: "wrap"})
        ui.Text(dateStr, {fontSize: 0.7})
    ))
)
###

refreshHeader: (changesTime: true) ->
    if changesTime
        if currentDoc'ok and currentDoc.category = "blog"
            headerTime.setSource(currentDoc.time).show()
        else
            headerTime.collapse()

    if inCompactMode
        headerTocButton.show()
    else
        headerTocButton.collapse()

    if headerTime.getVisibility() = "collapsed" and headerTocButton.getVisibility() = "collapsed"
        header.collapse()
    else
        header.show()

oldPath: null
uriUpdateHandler: <>
    path: decodeURI(location.pathname)
    if path ≠ oldPath

        contentElement.empty()
        commentsElement.empty()

        strippedPath: path.substr(1)
        if strippedPath.endsWith("/")
            strippedPath: strippedPath.substr(0, strippedPath.length - 1)
        doc: docsObj.(strippedPath)

        if doc'ok
            currentDoc: doc
            document.title: doc.title
            htmlString: md.render(doc.content)
            contentDom.innerHTML: parseElement("
                <div xmlns="http://www.w3.org/1999/xhtml">
                    \(htmlString)
                </div>
            ").innerHTML
            if client.browser.deviceType ≠ "computer"
                Array.from(contentDom.getElementsByTagName("a")).forEach(hyperlink ->
                    client.dom.initElementEvents(hyperlink)
                    href: hyperlink.getAttribute("href") # can't use `hyperlink.href` for it's not raw
                    hyperlink.addEventListener(client.dom.getCustomEventName("click"), e ->
                        if href.search(r"^(//|http:|https:)") ≠ -1
                            client.openLink(href)
                        else
                            client.setUri(href)
                    )
                )
            else
                Array.from(contentDom.getElementsByTagName("a")).forEach(hyperlink ->
                    href: hyperlink.getAttribute("href") # can't use `hyperlink.href` for it's not raw
                    hyperlink.addEventListener("click", e ->
                        if href.search(r"^(//|http:|https:)") = -1
                            e.preventDefault()
                            client.setUri(href)
                    )
                )
            addCommentHint.setSource(
                if currentDoc.category = "home"
                    ""
                else
                    lit.addCommentHint + currentDoc.id
            ).setSelectable(client.browser.deviceType = "computer")
        else
            currentDoc: null
            document.title: ""
            contentDom.textContent: ""
            addCommentHint.setSource("")

        refreshHeader()

        web.jsonGet("\(apiBase)/search/issues?q=\(encodeURIComponent(doc.id))+\
        in:title+-label:blocked+repo:\(username)/\(repo)&sort=created")
        .then(response ->
            issues: response.body.items
            if issues..isEmpty()
                commentsElement.add(
                    ui.Text(lit.noComment)
                )
            else
                issues.forEach issue ->
                    commentsElement.add(ui.Stack(
                        {
                            childAlignment: "start"
                            width: 1
                            height: "auto"
                            childSpacing: "0.3rem"
                        }
                        ui.Text(issue.user.login + ":")
                        ui.Text(issue.body, {wrapping: "wrap", width: 1, lineHeight: 1.35})
                        $button.Button(issue.comments + lit.commentReplyCountSuffix, {
                            fontSize: 0.8
                            click: <>
                                client.openLink(issue.html_url, true)
                        })
                    ))
        ).catch(<>
            commentsElement.add(
                ui.Text(lit.commentsLoadError, {wrapping: "wrap", width: 1, lineHeight: 1.35})
            )
        )

        oldPath: path

client.onUriChange(uriUpdateHandler)

resizeHandler: <>
    viewportSize: ui.getSize()

    tocLimit: Math.max(0, Math.floor((viewportSize.y - 18) / 3))

    if viewportSize.x / viewportSize.y < 1
        inCompactMode: true
        mainBody.setAspectRatio(null).setWidth(1.5).setAnchor(0.333333, 0.5).setX(0)
        leftCol.setWidth(0.333333)
        scrollElement.setWidth(0.666666)
    else
        inCompactMode: false
        if client.browser.deviceType = "handheld"
            mainBody.setAspectRatio(null).setWidth(1).setAnchor(0.5, 0.5).setX(0.5)
            leftCol.setWidth(0.3)
            scrollElement.setWidth(0.7)
        else
            if viewportSize.x / viewportSize.y < 1.33333
                mainBody.setAspectRatio(null).setWidth(1).setAnchor(0.5, 0.5).setX(0.5)
                leftCol.setWidth(0.22)
                scrollElement.setWidth(0.78)
            else
                mainBody.setAspectRatio(1.33333).setWidth("auto").setAnchor(0.5, 0.5).setX(0.5)
                leftCol.setWidth(0.22)
                scrollElement.setWidth(0.78)

    $toc.compactToggle(true) if $toc.compactSwitch
    $toc.refresh()
    refreshHeader(false)

ui.onResize(resizeHandler)

ui.root.onWheelStart(e ->
    if wheelEffective
        wheelEffective: false
        (
            if e.forward
                match sceneBox.getCurrentScene()
                    firstScene
                        firstSceneTitle.animate{
                            to: {translate: [0, ui.RemLength(-ui.getSize().y * 0.55)]}
                            duration: 400
                            timingFunction: "ease-in"
                        }
                        firstSceneHint.animate{
                            to: {translate: [0, ui.RemLength(-ui.getSize().y * 0.75)]}
                            delay: 200
                            duration: 400
                            timingFunction: "ease-in"
                        }.then(<>
                            secondSceneContent.setTranslate(0, 0)
                            sceneBox.showScene(secondScene, ui.transition.dissolve, 600)
                        )
                    secondScene
                        client.localData.homeLoaded: true
                        client.saveLocalData()
                        secondSceneContent.animate{
                            to: {translate: [0, ui.RemLength(-ui.getSize().y * 1)]}
                            duration: 400
                            timingFunction: "ease-in"
                        }.then(<>
                            sceneBox.showScene(mainScene, ui.transition.dissolve, 600)
                        )
                    mainScene
                        if ui.root.getWheelTarget() = null
                            ui.root.setWheelTarget(scrollElement)
                        Promise.resolve()
                    |
                        Promise.resolve()
            else
                match sceneBox.getCurrentScene()
                    secondScene
                        showFirstScene()
                    mainScene
                        if ui.root.getWheelTarget() = null
                            ui.root.setWheelTarget(scrollElement)
                        Promise.resolve()
                    |
                        Promise.resolve()
        ).then(<>
            wheelEffective: true
        )
)

resizeHandler()
uriUpdateHandler()
$toc.fillBlogs(searchResults..portion(0, tocLimit))

if client.localData.homeLoaded or location.pathname ≠ "/"
    ui.root.setWheelTarget(scrollElement)
    sceneBox.showScene(mainScene, ui.transition.dissolve, 600)
else
    showFirstScene()
