fus 2.0.5
import "babel-polyfill"
import "./shared.manifest" all

shared: import "site/shared"
#import "./shared.main"
client: import "site/client"

marked: import "marked"
#manuals: require("./client.manuals.coffee")
ui: client.ui

blogsObj: {}
originalTitle: document.title

parseElement: s -> DOMParser().parseFromString(s, "application/xml").documentElement

ui.setRem(0.02)

contentElement: ui.Element(
    {
        width: 0.8
        overflow: "scroll"
        lineHeight: 1.5
        selectable: true
        pointer: "auto"
        padding: "0.5rem"
    }
)
ui.root.add(
    ui.Stack(
        {
            width: "auto"
            aspectRatio: 1.25
            horizontal: true
        },
        ui.Stack{
            width: 0.2
            childSpacing: "0.3rem"
            padding: ui.Padding(0, "2rem", 0, 0)
        } as leftCol,
        contentElement
    )
)
Promise.all(client.text.map(blog ->
    if (blog.filename.endsWith(".md") or blog.filename.endsWith(".markdown"))
    and blog.content.startsWith("<script ")
        pos: blog.content.search("</script>")
        metaString: blog.content.substr(0, pos + "</script>".length)
        metaElement:
            DOMParser().parseFromString(metaString, "text/html")
            .querySelector("script[type=\"application/ld+json\"]")
        fileType: "markdown"
    (
        if metaElement'ok
            shared.jsonLd.compact(
                JSON.parse(metaElement.textContent),
                {"@context": "http://schema.org/"}
            )
        else
            Promise.resolve{
                headline: "no title"
                dateCreated: "2010-01-02T00:00Z"
            }
    ).then(meta ->
        {
            title: meta.headline
            time: Date(meta.dateCreated)
            type: fileType
            id: blog.filename.substr(0, blog.filename.indexOf("."))
            content: blog.content
        }
    )
)).then(blogs ->
    blogs..sortDescending(m -> m.time).forEach(blog ->
        if blog.id ≠ ""
            blogsObj.(blog.id): blog
        date: blog.time
        dateStr: date.getUTCFullYear()..pad(4) + "-"
            + (date.getUTCMonth() + 1)..pad(2) + "-"
            + date.getUTCDate()..pad(2)
        leftCol.add(ui.Stack(
            {
                height: "auto"
                padding: ui.Padding("0.5rem")
                backgroundPaint: "rgb(224,224,224)"
                pointer: "link"
                click: <> client.setUri("/" + blog.id)
            },
            ui.Text(blog.title),
            ui.Text(dateStr, {fontSize: 0.7})
        ))
    )

    oldPath: null
    uriUpdateHandler: <>
        path: decodeURI(location.pathname)
        if path ≠ oldPath

            contentElement.empty()

            if path = "/"
                document.title: originalTitle
                contentElement.raw.innerHTML: "这是詹臻臻的个人网站。"
            else
                strippedPath: path.substr(1)
                if strippedPath.endsWith("/")
                    strippedPath: strippedPath.substr(0, strippedPath.length - 1)
                blog: blogsObj.(strippedPath)
                document.title: blog.title
                htmlString: marked.parse(blog.content)
                contentElement.raw.innerHTML: parseElement("
                    <div xmlns="http://www.w3.org/1999/xhtml">
                        \(htmlString)
                    </div>
                ").innerHTML

            oldPath: path

    uriUpdateHandler()
    client.onUriChange(uriUpdateHandler)
)
