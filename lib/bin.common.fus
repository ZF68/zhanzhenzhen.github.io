fus 2.4.1
import "./shared.manifest" all

fs: import "fs"
githubInfo: import "./client.github-info"
githubCredential: require("../github-credential")

apiBase: "https://api.github.com"

headerFields: {
    "Authorization":
        "Basic "
        + (githubCredential.username + ":" + githubCredential.password)..toBytes()..toBase64()
    "If-Modified-Since": "Sat, 01 Jan 2000 00:00:00 GMT" # disable cache
    "User-Agent": "none" # this can be anything, we use "none"
}

validateName: name ->
    assert(name is String and 1 ≤ name.length ≤ 100 and name ≠ "." and name ≠ "..")
    assert(name.search(r"^[^'\"\\/:|*?.]+$") ≠ -1)
    assert(name.search(r"^[^\x00-\x1f\x7f]+$") ≠ -1)

validateIssueNumber: x -> assert(Number.isInteger(x) and 0 ≤ x ≤ 999999)

generateDocUri: (issueNumber, name) ->
    if githubInfo.repo.endsWith(".github.io")
        "https://\(githubInfo.repo)/\(issueNumber)-\(name)"
    else
        "https://\(githubInfo.owner).github.io/\(githubInfo.repo)/\(issueNumber)-\(name)"

updateIssue: (issueNumber, name) ->
    docUri: generateDocUri(issueNumber, name)
    web.request{
        method: "PATCH"
        uri: "\(apiBase)/repos/\(githubInfo.owner)/\(githubInfo.repo)/issues/\(issueNumber)"
        headerFields: Object..absorb({"Content-Type": "application/json"}, headerFields)
        body: JSON.stringify{
            title: name
            body: "[\(docUri)](\(docUri))"
        }
        responseBodyType: "json"
    }

add'export: (name: "no-title") ->
    validateName(name)
    issue: web.jsonPost(
        "\(apiBase)/repos/\(githubInfo.owner)/\(githubInfo.repo)/issues"
        {title: name}
        {headerFields: headerFields}
    )'wait.body
    updateIssue(issue.number, name)'wait
    path: "public-text/\(issue.number)-\(name).md"
    fs.writeFileSync(path, "")
    console.log("New file added:")
    console.log(path)

rename'export: (issueNumber, name) ->
    validateIssueNumber(issueNumber)
    validateName(name)
    oldName:
        fs.readdirSync("public-text")
        ..first(m -> m.startsWith("\(issueNumber)-") and m.endsWith(".md"))
        .match(r"^(.*)\.md$").1
    validateName(oldName)
    fs.renameSync("public-text/\(oldName).md", "public-text/\(issueNumber)-\(name).md")
    updateIssue(issueNumber, name)
